# sdl3_mixer cmake project-config input for ./configure scripts

include(FeatureSummary)
set_package_properties(SDL3_mixer PROPERTIES
    URL "https://www.libsdl.org/projects/SDL_mixer/"
    DESCRIPTION "SDL_mixer is a sample multi-channel audio mixer library"
)

set(SDL3_mixer_FOUND                TRUE)

set(SDL3MIXER_VENDORED              0)

set(SDL3MIXER_CMD                   @SDL3MIXER_CMD@)

set(SDL3MIXER_FLAC_LIBFLAC          @SDL3MIXER_FLAC_LIBFLAC@)
set(SDL3MIXER_FLAC_DRFLAC           @SDL3MIXER_FLAC_DRFLAC@)
if(SDL3MIXER_FLAC_LIBFLAC OR SDL3MIXER_FLAC_DRFLAC)
    set(SDL3MIXER_FLAC              1)
else()
    set(SDL3MIXER_FLAC              0)
endif()

set(SDL3MIXER_MOD_MODPLUG           @SDL3MIXER_MOD_MODPLUG@)
set(SDL3MIXER_MOD_XMP               @SDL3MIXER_MOD_XMP@)
set(SDL3MIXER_MOD_XMP_LITE          @SDL3MIXER_MOD_XMP_LITE@)
if(SDL3MIXER_MOD_MODPLUG OR SDL3MIXER_MOD_XMP OR SDL3MIXER_MOD_XMP_LITE)
    set(SDL3MIXER_MOD               1)
else()
    set(SDL3MIXER_MOD               0)
endif()

set(SDL3MIXER_MP3_DRMP3             @SDL3MIXER_MP3_DRMP3@)
set(SDL3MIXER_MP3_MPG123            @SDL3MIXER_MP3_MPG123@)
if(SDL3MIXER_MP3_DRMP3 OR SDL3MIXER_MP3_MPG123)
    set(SDL3MIXER_MP3               1)
else()
    set(SDL3MIXER_MP3               0)
endif()

set(SDL3MIXER_MIDI_FLUIDSYNTH       @SDL3MIXER_MIDI_FLUIDSYNTH@)
set(SDL3MIXER_MIDI_NATIVE           @SDL3MIXER_MIDI_NATIVE@)
set(SDL3MIXER_MIDI_TIMIDITY         @SDL3MIXER_MIDI_TIMIDITY@)
if(SDL3MIXER_MIDI_FLUIDSYNTH OR SDL3MIXER_MIDI_NATIVE OR SDL3MIXER_MIDI_TIMIDITY)
    set(SDL3MIXER_MIDI              1)
else()
    set(SDL3MIXER_MIDI              0)
endif()

set(SDL3MIXER_OPUS                  @SDL3MIXER_OPUS@)

set(SDL3MIXER_VORBIS)
set(SDL3MIXER_VORBIS_STB            @SDL3MIXER_VORBIS_STB@)
set(SDL3MIXER_VORBIS_VORBISFILE     @SDL3MIXER_VORBIS_VORBISFILE@)
set(SDL3MIXER_VORBIS_TREMOR         @SDL3MIXER_VORBIS_TREMOR@)
if(SDL3MIXER_VORBIS_STB)
    set(SDL3MIXER_VORBIS            STB)
endif()
if(SDL3MIXER_VORBIS_VORBISFILE)
    set(SDL3MIXER_VORBIS            VORBISFILE)
endif()
if(SDL3MIXER_VORBIS_TREMOR)
    set(SDL3MIXER_VORBIS            TREMOR)
endif()

set(SDL3MIXER_WAVE                  @SDL3MIXER_WAVE@)

set(SDL3MIXER_WAVPACK               @SDL3MIXER_WAVPACK@)

set(SDL3MIXER_SDL3_REQUIRED_VERSION @SDL_VERSION@)

get_filename_component(prefix "${CMAKE_CURRENT_LIST_DIR}/@cmake_prefix_relpath@" ABSOLUTE)
set(exec_prefix "@exec_prefix@")
set(bindir "@bindir@")
set(includedir "@includedir@")
set(libdir "@libdir@")
set(_sdl3mixer_extra_static_libraries "@MIXER_LIBS@ @PC_LIBS@")
string(STRIP "${_sdl3mixer_extra_static_libraries}" _sdl3mixer_extra_static_libraries)

set(_sdl3mixer_bindir   "${bindir}")
set(_sdl3mixer_libdir   "${libdir}")
set(_sdl3mixer_incdir   "${includedir}/SDL3")

# Convert _sdl3mixer_extra_static_libraries to list and keep only libraries
string(REGEX MATCHALL "(-[lm]([-a-zA-Z0-9._]+))|(-Wl,[^ ]*framework[^ ]*)" _sdl3mixer_extra_static_libraries "${_sdl3mixer_extra_static_libraries}")
string(REGEX REPLACE "^-l" "" _sdl3mixer_extra_static_libraries "${_sdl3mixer_extra_static_libraries}")
string(REGEX REPLACE ";-l" ";" _sdl3mixer_extra_static_libraries "${_sdl3mixer_extra_static_libraries}")

unset(prefix)
unset(exec_prefix)
unset(bindir)
unset(includedir)
unset(libdir)

include(CMakeFindDependencyMacro)

if(NOT TARGET SDL3_mixer::SDL3_mixer)
    if(WIN32)
        set(_sdl3mixer_dll "${_sdl3mixer_bindir}/SDL3_mixer.dll")
        set(_sdl3mixer_imp "${_sdl3mixer_libdir}/${CMAKE_STATIC_LIBRARY_PREFIX}SDL3_mixer.dll${CMAKE_STATIC_LIBRARY_SUFFIX}")
        if(EXISTS "${_sdl3mixer_dll}" AND EXISTS "${_sdl3mixer_imp}")
            add_library(SDL3_mixer::SDL3_mixer SHARED IMPORTED)
            set_target_properties(SDL3_mixer::SDL3_mixer
                PROPERTIES
                    IMPORTED_LOCATION "${_sdl3mixer_dll}"
                    IMPORTED_IMPLIB "${_sdl3mixer_imp}"
            )
        endif()
        unset(_sdl3mixer_dll)
        unset(_sdl3mixer_imp)
    else()
        set(_sdl3mixer_shl "${_sdl3mixer_libdir}/${CMAKE_SHARED_LIBRARY_PREFIX}SDL3_mixer${CMAKE_SHARED_LIBRARY_SUFFIX}")
        if(EXISTS "${_sdl3mixer_shl}")
            add_library(SDL3_mixer::SDL3_mixer SHARED IMPORTED)
            set_target_properties(SDL3_mixer::SDL3_mixer
                PROPERTIES
                    IMPORTED_LOCATION "${_sdl3mixer_shl}"
            )
        endif()
    endif()
    if(TARGET SDL3_mixer::SDL3_mixer)
        set_target_properties(SDL3_mixer::SDL3_mixer
            PROPERTIES
                INTERFACE_INCLUDE_DIRECTORIES "${_sdl3mixer_incdir}"
                COMPATIBLE_INTERFACE_BOOL "SDL3_SHARED"
                INTERFACE_SDL3_SHARED "ON"
        )
    endif()
endif()

if(NOT TARGET SDL3_mixer::SDL3_mixer-static)
    set(_sdl3mixer_stl "${_sdl3mixer_libdir}/${CMAKE_STATIC_LIBRARY_PREFIX}SDL3_mixer${CMAKE_STATIC_LIBRARY_SUFFIX}")
    if(EXISTS "${_sdl3mixer_stl}")
        add_library(SDL3_mixer::SDL3_mixer-static STATIC IMPORTED)
        set_target_properties(SDL3_mixer::SDL3_mixer-static
            PROPERTIES
                INTERFACE_INCLUDE_DIRECTORIES "${_sdl3mixer_incdir}"
                IMPORTED_LOCATION "${_sdl3mixer_stl}"
                INTERFACE_LINK_LIBRARIES "${_sdl3mixer_extra_static_libraries}"
        )
    endif()
    unset(_sdl3mixer_stl)
endif()

unset(_sdl3mixer_extra_static_libraries)
unset(_sdl3mixer_bindir)
unset(_sdl3mixer_libdir)
unset(_sdl3mixer_incdir)
